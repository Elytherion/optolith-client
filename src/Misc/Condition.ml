module Dynamic = struct
  type value = One | Two | Three | Four

  type t = { id : int; value : value }
end

module Static = struct
  type t = {
    id : int;
    name : string;
    description : string option;
    levelDescription : string option;
    levels : string * string * string * string;
    src : PublicationRef.list;
    errata : Erratum.list;
  }

  module Decode = Json_Decode_Static.Make (struct
    type nonrec t = t

    module Translation = struct
      type t = {
        name : string;
        description : string option;
        levelDescription : string option;
        levels : string * string * string * string;
        errata : Erratum.list option;
      }

      let t json =
        Json_Decode_Strict.
          {
            name = json |> field "name" string;
            description = json |> optionalField "description" string;
            levelDescription = json |> optionalField "levelDescription" string;
            levels = json |> field "levels" (tuple4 string string string string);
            errata = json |> optionalField "errata" Erratum.Decode.list;
          }

      let pred _ = true
    end

    type multilingual = {
      id : int;
      src : PublicationRef.Decode.multilingual list;
      translations : Translation.t Json_Decode_TranslationMap.partial;
    }

    let multilingual decodeTranslations json =
      Json_Decode_Strict.
        {
          id = json |> field "id" int;
          src = json |> field "src" PublicationRef.Decode.multilingualList;
          translations = json |> field "translations" decodeTranslations;
        }

    let make langs (multilingual : multilingual) (translation : Translation.t) =
      Some
        {
          id = multilingual.id;
          name = translation.name;
          description = translation.description;
          levelDescription = translation.levelDescription;
          levels = translation.levels;
          src =
            PublicationRef.Decode.resolveTranslationsList langs multilingual.src;
          errata = translation.errata |> Ley_Option.fromOption [];
        }

    module Accessors = struct
      let id (x : t) = x.id

      let translations x = x.translations
    end
  end)
end
